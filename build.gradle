plugins {
    // Provide convenience executables for trying out the examples.
    id 'application'
    // ASSUMES GRADLE 5.6 OR HIGHER. Use plugin version 0.8.10 with earlier gradle versions
    id 'com.google.protobuf' version '0.8.18'
    // Generate IntelliJ IDEA's .idea & .iml project files
    id 'idea'
}

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/maven2/" }
    mavenCentral()
    mavenLocal()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// IMPORTANT: You probably want the non-SNAPSHOT version of gRPC. Make sure you
// are looking at a tagged version of the example and not "master"!

// Feel free to delete the comment at the next line. It is just for safely
// updating the version in our release process.
def grpcVersion = '1.51.1' // CURRENT_GRPC_VERSION
def protobufVersion = '3.21.7'
def protocVersion = protobufVersion

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    // https://mvnrepository.com/artifact/org.json/json
    implementation 'org.json:json:20220924'
    // https://mvnrepository.com/artifact/org.eclipse.paho/org.eclipse.paho.client.mqttv3
    implementation group: 'org.eclipse.paho', name: 'org.eclipse.paho.client.mqttv3', version: '1.2.5'
    // https://mvnrepository.com/artifact/org.apache.camel/camel-paho
    implementation group: 'org.apache.camel', name: 'camel-paho', version: '3.20.1'

    // https://mvnrepository.com/artifact/javax.inject/javax.inject
    implementation group: 'javax.inject', name: 'javax.inject', version: '1'
    // https://mvnrepository.com/artifact/org.apache.camel/camel-core
    implementation group: 'org.apache.camel', name: 'camel-core', version: '3.20.1'
    // https://mvnrepository.com/artifact/org.apache.camel/camel-grpc
    implementation group: 'org.apache.camel', name: 'camel-grpc', version: '3.20.1'




    compileOnly "org.apache.tomcat:annotations-api:6.0.53"

    // examples/advanced need this for JsonFormat
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:3.4.0"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

startScripts.enabled = false

task helloWorldServer(type: CreateStartScripts) {
    mainClass = 'io.grpc.examples.helloworld.HelloWorldServer'
    applicationName = 'riddle-server'
    outputDir = new File(project.buildDir, 'tmp/scripts/' + name)
    classpath = startScripts.classpath
}

task helloWorldClient(type: CreateStartScripts) {
    mainClass = 'io.grpc.examples.helloworld.HelloWorldClient'
    applicationName = 'riddle-client'
    outputDir = new File(project.buildDir, 'tmp/scripts/' + name)
    classpath = startScripts.classpath
}

task CamelClient(type: CreateStartScripts) {
    mainClass = 'io.grpc.examples.helloworld.CamelClient'
    applicationName = 'camel-client'
    outputDir = new File(project.buildDir, 'tmp/scripts/' + name)
    classpath = startScripts.classpath
}


applicationDistribution.into('bin') {
    from(helloWorldServer)
    from(helloWorldClient)
    from(CamelClient)

    fileMode = 0755
}
